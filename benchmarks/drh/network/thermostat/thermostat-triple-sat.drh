
#define K1 0.015
#define K2 0.045
#define K3 0.03
#define H1 100.0
#define H2 200.0
#define H3 300.0

#define C12  0.01
#define C23  0.05
#define C31  0.02

#define gT 20

[0, 1] time;

(component thermostat3;

[-20, 100] x1;
[-20, 100] x2;
[-20, 100] x3;

[0, 1] k;
[100, 300] h;
[0, 1] c1;
[0, 1] c2;



[0, 1] tau;

(mode on;

  invt: (tau >= 0);
        (tau <= 1);
  flow:
        d/dt[x1] = k * (h - ((1 - c1 - c2) * x1 + c1 * x2 + c2 * x3));
        d/dt[x2] = 0;
        d/dt[x3] = 0;
        d/dt[k] = 0;
        d/dt[h] = 0;
        d/dt[c1] = 0;
        d/dt[c2] = 0;

        d/dt[tau]  = 1;
  jump:
        () : (and (tau = 1) (x1 <= gT)) ==> @on (tau' = 0);
        () : (and (tau = 1) (x1 > gT)) ==>  @off (tau' = 0);
)

(mode off;

  invt: (tau >= 0);
        (tau <= 1);
  flow:
        d/dt[x1] = -k * ((1 - c1 - c2) * x1 + c1 * x2 + c2 * x3);
        d/dt[x2] = 0;
        d/dt[x3] = 0;
        d/dt[k] = 0;
        d/dt[h] = 0;
        d/dt[c1] = 0;
        d/dt[c2] = 0;
        d/dt[tau]  = 1;
  jump:
        () : (and (tau = 1) (x1 <= gT)) ==> @on (tau' = 0);
        () : (and (tau = 1) (x1 > gT)) ==>  @off (tau' = 0);
)
)



analyze:
 t1 = thermostat3[[x1/x1,x2/x2,x3/x3,k/k1,h/h1,c1/c12,c2/c31],
                  @on (and (x1 >= gT - 1) (x1 <= gT + 1) (tau = 0)
		           (k1 = K1) (h1 = H1) (c12 = C12) (c31 = C31))];
 t2 = thermostat3[[x1/x2,x2/x1,x3/x3,k/k2,h/h2,c1/c12,c2/c23],
                  @on (and (x2 >= gT - 1) (x2 <= gT + 1) (tau = 0)
		           (k2 = K2) (h2 = H2) (c12 = C12) (c23 = C23))];
 t3 = thermostat3[[x1/x3,x2/x1,x3/x2,k/k3,h/h3,c1/c23,c2/c31],
                  @on (and (x3 >= gT - 1) (x3 <= gT + 1) (tau = 0)
		           (k3 = K3) (h3 = H3) (c23 = C23) (c31 = C31))];
  (t1 || t2 || t3);

goal:
	() : (and (or (x1 < gT - 1) (x1 > gT + 1)) (or (x2 < gT - 1) (x2 > gT + 1)) (or (x3 < gT - 1) (x3 > gT + 1)));

